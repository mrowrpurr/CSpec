# Immediately run all the tests! As part of the CMake generation. Fast feedback :)
function(cspec_runner_immediate)
    __cspec_arg_parse(VALUES SCOPE ARGS ${ARGN})
    get_property(test_files ${${arg_prefix}SCOPE} PROPERTY CSPEC_TEST_FILES)

    set(passed_count 0)
    set(failed_count 0)
    foreach(file_path ${test_files})
        string(SHA1 file_id "${file_path}")
        get_property(test_fns ${${arg_prefix}SCOPE} PROPERTY CSPEC_TEST_FUNCTIONS_${file_id})
        get_property(setup_fns ${${arg_prefix}SCOPE} PROPERTY CSPEC_TEST_SETUP_FUNCTIONS_${file_id})
        get_property(teardown_fns ${${arg_prefix}SCOPE} PROPERTY CSPEC_TEST_TEARDOWN_FUNCTIONS_${file_id})
        get_property(file_setup_fns ${${arg_prefix}SCOPE} PROPERTY CSPEC_TEST_FILE_SETUP_FUNCTIONS_${file_id})
        get_property(file_teardown_fns ${${arg_prefix}SCOPE} PROPERTY CSPEC_TEST_FILE_TEARDOWN_FUNCTIONS_${file_id})

        get_filename_component(test_filename "${file_path}" NAME_WLE)

        list(LENGTH test_fns test_fn_count)
        if(test_fn_count)
            message("\n${test_filename} (${test_fn_count} tests)")
        else()
            message("\n${test_filename} (No tests)")
        endif()

        # Just do tests first...
        foreach(test_fn ${test_fns})
            execute_process(COMMAND "${CMAKE_COMMAND}" "-DCSPEC_FILE=${file_path}" "-DCSPEC_TEST_FUNCTION=${test_fn}" -P "${CSpecModuleIncludes}/bin/RunFileTestFunction.cmake" OUTPUT_VARIABLE stdout ERROR_VARIABLE stderr RESULT_VARIABLE result)
            if(result EQUAL 0)
                math(EXPR passed_count "${passed_count}+1")
                message("    [PASS] ${test_fn}")
                # if some DEBUG is set, then print everything
            else()
                math(EXPR failed_count "${failed_count}+1")
                message("    [FAIL] ${test_fn}")
                __cspec_pretty_print_output("${stderr}")
            endif()
        endforeach()
    endforeach()

    if(failed_count GREATER 0)
        message("\nests Failed! ${passed_count} passed, ${failed_count} failed.\n")
    else()
        message("\nTests Passed! ${passed_count} passed.\n")
    endif()
endfunction()