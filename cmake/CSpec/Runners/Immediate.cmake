# Immediately run all the tests! As part of the CMake generation. Fast feedback :)
function(cspec_runner_immediate)
    __cspec_arg_parse(VALUES SCOPE ARGS ${ARGN})
    get_property(test_files ${${arg_prefix}SCOPE} PROPERTY CSPEC_TEST_FILES)

    set(passed_count 0)
    set(failed_count 0)
    foreach(file_path ${test_files})
        string(SHA1 file_id "${file_path}")
        get_property(test_fns ${${arg_prefix}SCOPE} PROPERTY CSPEC_TEST_FUNCTIONS_${file_id})
        get_property(skip_test_fns ${${arg_prefix}SCOPE} PROPERTY CSPEC_SKIP_TEST_FUNCTIONS_${file_id})
        get_property(setup_fns ${${arg_prefix}SCOPE} PROPERTY CSPEC_TEST_SETUP_FUNCTIONS_${file_id})
        get_property(teardown_fns ${${arg_prefix}SCOPE} PROPERTY CSPEC_TEST_TEARDOWN_FUNCTIONS_${file_id})
        get_property(file_setup_fns ${${arg_prefix}SCOPE} PROPERTY CSPEC_TEST_FILE_SETUP_FUNCTIONS_${file_id})
        get_property(file_teardown_fns ${${arg_prefix}SCOPE} PROPERTY CSPEC_TEST_FILE_TEARDOWN_FUNCTIONS_${file_id})

        get_filename_component(test_filename "${file_path}" NAME_WLE)

        list(LENGTH test_fns test_fn_count)
        if(test_fn_count)
            __cspec_output("\n${test_filename} (${test_fn_count} tests)")
        else()
            __cspec_output("\n${test_filename} (No tests)")
        endif()

        foreach(skipped_test_fn ${skip_test_fns})
            __cspec_output("    [SKIP] ${skipped_test_fn}")
        endforeach()

        # TODO: setup, teardown

        foreach(test_fn ${test_fns})
            execute_process(COMMAND "${CMAKE_COMMAND}" "-DCSPEC_FILE=${file_path}" "-DCSPEC_TEST_FUNCTION=${test_fn}" -P "${CSpecModuleIncludes}/bin/RunFileTestFunction.cmake" OUTPUT_VARIABLE stdout ERROR_VARIABLE stderr RESULT_VARIABLE result)
            if(result EQUAL 0)
                math(EXPR passed_count "${passed_count}+1")
                __cspec_output("    [PASS] ${test_fn}")
                if(CSPEC_VERBOSE)
                    __cspec_pretty_print_output("${stdout}")
                    __cspec_pretty_print_output("${stderr}")
                endif()
            else()
                math(EXPR failed_count "${failed_count}+1")
                __cspec_output("    [FAIL] ${test_fn}")
                __cspec_pretty_print_output("${stdout}")
                __cspec_pretty_print_output("${stderr}")
            endif()
        endforeach()
    endforeach()

    if(failed_count GREATER 0)
        __cspec_output("\nests Failed! ${passed_count} passed, ${failed_count} failed.\n")
    elseif(passed_count GREATER 0)
        __cspec_output("\nTests Passed! ${passed_count} passed.\n")
    else()
        __cspec_output("\nNo tests run\n")
    endif()
endfunction()